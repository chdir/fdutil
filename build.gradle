// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
    }
}

allprojects {
    repositories {
        jcenter()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
    }
}

configurations.all {
    resolutionStrategy {
        // fail eagerly on version conflict (includes transitive dependencies)
        // e.g. multiple different versions of the same dependency (group and name are equal)
        failOnVersionConflict()

        // prefer modules that are part of this build (multi-project or composite build) over external modules
        preferProjectModules()
    }
}

if (!project.hasProperty('deploymentProps')) {
    ext.deploymentProps = new Properties();

    def propFile

    def propName = project.properties.deploymentConfig

    if(propName && (propFile = file(propName)).exists()) {
        new FileInputStream(propFile).withStream { InputStream is ->
            deploymentProps.load(is)
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

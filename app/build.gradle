apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion '26.0.2'

    if (deploymentProps) {
        signingConfigs {
            release {
                storeFile file(deploymentProps['xfdStoreFile'])
                keyAlias deploymentProps['xfdKeyAlias']

                storePassword deploymentProps['xfdStorePassword']
                keyPassword deploymentProps['xfdKeyPassword']
            }
        }
    }

    defaultConfig {
        applicationId 'net.sf.fakenames.fddemo'
        minSdkVersion 19
        targetSdkVersion 24
        versionCode 1
        versionName '1.0'

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        testInstrumentationRunnerArguments [['disableAnalytics' : 'true']]

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath = true
            }
        }

        aaptOptions {
            additionalParameters "--no-version-vectors"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        qa {
            minifyEnabled true
            proguardFiles 'proguard-rules.pro'

            if (deploymentProps) {
                signingConfig signingConfigs.release
            }

            matchingFallbacks = ['debug', 'release']
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        checkAllWarnings true

        error 'EasterEgg'
    }
}

dependencies {
    //noinspection GradleCompatible
    compile 'com.android.support:recyclerview-v7:25.0.1'
    //noinspection GradleCompatible
    compile 'com.android.support:appcompat-v7:27.0.2'
    //noinspection GradleCompatible
    compile 'com.android.support:design:27.0.2'

    compile 'org.parceler:parceler-api:1.1.6'
    annotationProcessor 'org.parceler:parceler:1.1.6'

    compile 'com.android.support:support-annotations:25.4.0'

    compile 'com.jakewharton:butterknife:7.0.1'

    androidTestCompile 'com.android.support.test:runner:1.0.1'
    androidTestCompile 'com.android.support.test:rules:1.0.1'
    androidTestCompile 'junit:junit:4.12'

    androidTestCompile 'com.google.guava:guava:19.0'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    compile project(':provider')

    compile 'com.squareup.okhttp3:okhttp:3.8.0'
    compile 'com.squareup.okhttp3:okhttp-urlconnection:3.8.0'
    compile 'com.squareup.okhttp3:logging-interceptor:+'

    androidTestCompile project(':provider')
    androidTestCompile 'com.google.truth:truth:0.39'
    androidTestCompile 'com.squareup.okio:okio:1.13.0'

    testCompile project(':provider')
    testCompile 'org.robolectric:robolectric:3.2.2'
    testCompile 'junit:junit:4.12'
}
